plugins {
	id 'version-catalog'
	id 'shared'
}

managedVersioning {
	gitHubActions {
		release {
			prettyName.set 'Release'
			workflowDispatch.set(true)
			gradleJob {
				name.set 'build'
				step {
					setupGitUser()
				}
				buildCache()
				readOnly.set false
				gradlew 'Tag Release', 'tagRelease'
				gradlew 'Build', 'build'
				step {
					run.set 'git push && git push --tags'
				}
				gradlew 'Publish', 'publish'
				mavenRelease('github')
			}
		}
		build_pr {
			prettyName.set 'Build PR'
			pullRequest.set(true)
			gradleJob {
				name.set 'build'
				gradlew 'Build', 'build'
				gradlew 'Publish', 'publish'
				pullRequestArtifact()
			}
		}
		publish_pr {
			prettyName.set 'Publish PR'
			publishPullRequestAction(
				'github',
				'dev/lukebemish/conventions/conventions-*',
				'Build PR'
			)
		}
	}
}

println "Building: $version"

dependencies {
	implementation libs.gradle.foojay
	implementation libs.gradle.develocity
}

gradlePlugin {
	plugins {
		sharedPlugin {
			id = 'dev.lukebemish.conventions'
			implementationClass = 'dev.lukebemish.conventions.ConventionsPlugin'
		}
	}
}

catalog {
	versionCatalog {
		from(files("gradle/libs.versions.toml"))
		version 'conventions', project.version as String
		library('conventions.settings', 'dev.lukebemish', 'conventions').versionRef('conventions')
		library('conventions.java', 'dev.lukebemish.conventions', 'java').versionRef('conventions')
		library('conventions.buildsrc', 'dev.lukebemish.conventions', 'conventionplugin').versionRef('conventions')
		plugin('conventions.settings', 'dev.lukebemish.conventions').versionRef('conventions')
		plugin('conventions.java', 'dev.lukebemish.conventions.java').versionRef('conventions')
		plugin('conventions.buildsrc', 'dev.lukebemish.conventions.buildsrc').versionRef('conventions')
	}
}

AdhocComponentWithVariants javaComponent = (AdhocComponentWithVariants) project.components.named("java").get()
javaComponent.addVariantsFromConfiguration(configurations.versionCatalogElements) {}
